"""
Snakemake pipeline that is essentially a wrapper for apply_to_query.py. 
"""

import os

# ============================================================================
# Configuration (loaded from config file)
# ============================================================================

LOG_DIR = config.get("log_dir", "logs")
os.makedirs(LOG_DIR, exist_ok=True)

# Directories
CODE_DIR = config["code_dir"]
OUT_DIR = config["out_dir"]
RNA_DIR = config["rna_dir"]
ATAC_DIR = config["atac_dir"]

# Samplesheet
SAMPLESHEET = config["samplesheet"]

# Parameters
PEAK_SET = config["peak_set"]
ATAC_C = config["atac_C"]
GENE_SET = config["gene_set"]
RNA_C = config["rna_C"]
DEFAULT_C = config["default_C"]
EVEN_BARPLOT = config.get("even_barplot", True)
S = config.get("S", 1)
COR_THRESHOLD = config.get("cor_threshold", 0.05)

# Conda environments
SNAPATAC2_ENV = config["snapatac2_env"]

# ============================================================================
# Target rule
# ============================================================================
names = ["custom", "hv_small", "hv_med", "hv_large"]
all_inputs = []
for name in names:
    bar_plot = os.path.join(OUT_DIR, f"apply_{name}", "sorted_barplot_median_adjusted.png")
    shap_vals = os.path.join(OUT_DIR, f"apply_{name}", "shap", "atac_shap_vals.csv")
    rna_scc_feats = os.path.join(OUT_DIR, f"apply_{name}", "top_features", "rna_active_SCC_features.csv")
    linked_atac_feats = os.path.join(OUT_DIR, f"apply_{name}", "top_features", "atac_active_SCC_features_linked.csv")
    rna_preds_on_umap = os.path.join(OUT_DIR, f"apply_{name}", "SCC_RNA_probs_on_umap.png")
    knn_devs = os.path.join(OUT_DIR, f"apply_{name}", "SCC_probs_knn_devs.csv")
    all_inputs.append(bar_plot)
    all_inputs.append(shap_vals)
    all_inputs.append(rna_scc_feats)
    all_inputs.append(linked_atac_feats)
    all_inputs.append(rna_preds_on_umap)
    all_inputs.append(knn_devs)

rule all:
    input:
        all_inputs

def make_apply_to_query_rule(name, peak_set, gene_set, atac_C, rna_C, even_barplot):
    rule:
        name: f"apply_to_query_{name}"
        input:
            atac_dir = ATAC_DIR,
            rna_dir = RNA_DIR, 
        output:
            barplot = os.path.join(OUT_DIR, f"apply_{name}", "sorted_barplot_median_adjusted.png"),
            shap_vals = os.path.join(OUT_DIR, f"apply_{name}", "shap", "atac_shap_vals.csv"),
            coefs = os.path.join(OUT_DIR, f"apply_{name}", "shap", "atac_coefs.csv")
        params:
            code_dir = CODE_DIR,
            out_dir = os.path.join(OUT_DIR, f"apply_{name}"),
            atac_C = atac_C,
            rna_C = rna_C,
            peak_set = peak_set,
            gene_set = gene_set,
            even_barplot_flag = "--even_barplot" if even_barplot else "",
        conda: SNAPATAC2_ENV
        threads: 6
        resources:
            mem_mb = 48000,
            runtime = 120,
        log:
            os.path.join(LOG_DIR, f"apply_to_query_{name}.log")
        shell:
            """
            python -u {params.code_dir}/apply_to_query.py \
                {params.out_dir} \
                {input.atac_dir}/query_log2cpm.rds \
                {input.rna_dir}/query_log2cpm.rds \
                {input.atac_dir}/{params.peak_set} \
                {params.atac_C} \
                {input.rna_dir}/{params.gene_set} \
                {params.rna_C} \
                {params.even_barplot_flag} \
                2>&1 | tee {log}
            """

make_apply_to_query_rule("custom", PEAK_SET, GENE_SET, ATAC_C, RNA_C, EVEN_BARPLOT)
make_apply_to_query_rule("hv_small", "hvp_5000", "hvg_500", DEFAULT_C, DEFAULT_C, EVEN_BARPLOT)
make_apply_to_query_rule("hv_med", "hvp_10000", "hvg_3000", DEFAULT_C, DEFAULT_C, EVEN_BARPLOT)
make_apply_to_query_rule("hv_large", "hvp_50000", "hvg_5000", DEFAULT_C, DEFAULT_C, EVEN_BARPLOT)

def make_get_top_features_rule(name, S):
    rule:
        name: f"get_top_features_{name}"
        input:
            os.path.join(OUT_DIR, f"apply_{name}", "shap", "atac_shap_vals.csv")
        output:
            os.path.join(OUT_DIR, f"apply_{name}", "top_features", "rna_active_SCC_features.csv"),
            os.path.join(OUT_DIR, f"apply_{name}", "top_features", "atac_active_SCC_features.csv")
        params:
            code_dir = CODE_DIR,
            shap_dir = os.path.join(OUT_DIR, f"apply_{name}", "shap"),
            out_dir = os.path.join(OUT_DIR, f"apply_{name}", "top_features"),
            S = S,
            probs_file = os.path.join(OUT_DIR, f"apply_{name}", "SCC_probs_median_adjusted.csv")
        conda: SNAPATAC2_ENV
        threads: 6
        resources:
            mem_mb = 48000,
            runtime = 120,
        log:
            os.path.join(LOG_DIR, f"get_top_features_{name}.log")
        shell:
            """
            python -u {params.code_dir}/get_top_features.py \
                {params.out_dir} \
                {params.shap_dir} \
                {params.probs_file} \
                --S {params.S} \
                2>&1 | tee {log}
            """

make_get_top_features_rule("custom", S)
make_get_top_features_rule("hv_small", S)
make_get_top_features_rule("hv_med", S)
make_get_top_features_rule("hv_large", S)

def make_link_top_atac_features_rule(name, peak_set, cor_threshold):
    rule:
        name: f"link_top_atac_features_{name}"
        input:
            os.path.join(OUT_DIR, f"apply_{name}", "top_features", "atac_active_SCC_features.csv"),
        output:
            os.path.join(OUT_DIR, f"apply_{name}", "top_features", "atac_active_SCC_features_linked.csv"),
        params:
            code_dir = CODE_DIR,
            atac_lognorm_path = os.path.join(ATAC_DIR, "query_log2cpm.rds"),
            rna_lognorm_path = os.path.join(RNA_DIR, "query_log2cpm.rds"),
            peak_set_path = os.path.join(ATAC_DIR, peak_set, "peaks.txt"),
            top_features_dir = os.path.join(OUT_DIR, f"apply_{name}", "top_features"),
            out_dir = os.path.join(OUT_DIR, f"apply_{name}"),
            cor_threshold = cor_threshold,
        conda: SNAPATAC2_ENV
        threads: 6
        resources:
            mem_mb = 48000,
            runtime = 120,
        log:
            os.path.join(LOG_DIR, f"link_top_atac_features_{name}.log")
        shell:
            """
            python -u {params.code_dir}/link_top_atac_features.py \
                {params.atac_lognorm_path} \
                {params.rna_lognorm_path} \
                {params.peak_set_path} \
                {params.top_features_dir} \
                {params.out_dir} \
                --cor_threshold {params.cor_threshold} \
                2>&1 | tee {log}
            """

make_link_top_atac_features_rule("custom", PEAK_SET, COR_THRESHOLD)
make_link_top_atac_features_rule("hv_small", "hvp_5000", COR_THRESHOLD)
make_link_top_atac_features_rule("hv_med", "hvp_10000", COR_THRESHOLD)
make_link_top_atac_features_rule("hv_large", "hvp_50000", COR_THRESHOLD)


def make_plot_preds_on_umap_rule(name):
    rule:
        name: f"plot_preds_on_umap_{name}"
        input:
            scc_probs_csv = os.path.join(OUT_DIR, f"apply_{name}", "SCC_probs_median_adjusted.csv"),
        output:
            rna_preds_on_umap = os.path.join(OUT_DIR, f"apply_{name}", "SCC_RNA_probs_on_umap.png"),
            atac_preds_on_umap = os.path.join(OUT_DIR, f"apply_{name}", "SCC_ATAC_probs_on_umap.png"),
            knn_devs = os.path.join(OUT_DIR, f"apply_{name}", "SCC_probs_knn_devs.csv")
        params:
            code_dir = CODE_DIR,
            out_dir = os.path.join(OUT_DIR, f"apply_{name}"),
            samplesheet = SAMPLESHEET,
        conda: SNAPATAC2_ENV
        threads: 6
        resources:
            mem_mb = 48000,
            runtime = 120,
        log:
            os.path.join(LOG_DIR, f"plot_preds_on_umap_{name}.log")
        shell:
            """
            python -u {params.code_dir}/plot_preds_on_umap.py \
                {params.samplesheet} \
                {input.scc_probs_csv} \
                {params.out_dir} \
                2>&1 | tee {log}
            """

make_plot_preds_on_umap_rule("custom")
make_plot_preds_on_umap_rule("hv_small")
make_plot_preds_on_umap_rule("hv_med")
make_plot_preds_on_umap_rule("hv_large")
