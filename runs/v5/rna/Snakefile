"""
Snakemake pipeline for TCGA RNA-seq modeling.

Pipeline steps:
    1.1 Construct pseudobulks from query data
    1.2 Log-normalize query pseudobulks
    1.3 Create input gene sets for modeling
    1.4 Train RNA logistic regression models (per gene set)
    1.5 Plot model performances
"""

import os

# ============================================================================
# Configuration
# ============================================================================
LOG_DIR = config["log_dir"]
os.makedirs(LOG_DIR, exist_ok=True)

# Directories
CODE_DIR = config["code_dir"]
OUT_DIR = config["out_dir"]
GENE_SETS_DIR = os.path.join(OUT_DIR, "gene_sets")

# Input files
TCGA_XENA_PATH = config["tcga_xena_path"]
TCGA_CLINDATA_PATH = config["tcga_clindata_path"]
SAMPLESHEET = config["samplesheet"]

# Conda environments
SNAPATAC2_ENV = "/home/yangf4/envs/SnapATAC2"
INFERCNV_ENV = "/home/yangf4/envs/infercnv"

# Gene sets to process (will be determined dynamically after step 1.3)
# These are the expected gene set names based on create_input_gene_sets.py
GENE_SETS = config["gene_sets"]

# ============================================================================
# Target rule
# ============================================================================

rule all:
    input:
        os.path.join(OUT_DIR, "all_models_cv_metrics.png"),
        os.path.join(OUT_DIR, "query_log2cpm.rds")

# ============================================================================
# Step 1.1: Construct pseudobulks
# ============================================================================

rule construct_pseudobulks:
    """Construct query pseudobulks from single-cell RNA data."""
    input:
        samplesheet = SAMPLESHEET,
    output:
        pseudobulks = os.path.join(OUT_DIR, "query_pseudobulks.mtx"),
        group_names = os.path.join(OUT_DIR, "query_group_names.txt"),
        genes = os.path.join(OUT_DIR, "query_genes.txt"),
    params:
        code_dir = CODE_DIR,
        out_dir = OUT_DIR,
    conda: SNAPATAC2_ENV
    threads: 6
    resources:
        mem_mb = 48000,
        runtime = 120,
    log:
        os.path.join(LOG_DIR, "construct_pseudobulks.log")
    shell:
        """
        python -u {params.code_dir}/construct_query_pseudobulks.py \
            {input.samplesheet} \
            {params.out_dir} \
            2>&1 | tee {log}
        """

# ============================================================================
# Step 1.2: Log-normalize pseudobulks
# ============================================================================

rule log_normalize_pseudobulks:
    """Log-normalize query pseudobulks."""
    input:
        query_pseudobulks = os.path.join(OUT_DIR, "query_pseudobulks.mtx"),
        query_group_names = os.path.join(OUT_DIR, "query_group_names.txt"),
        query_genes = os.path.join(OUT_DIR, "query_genes.txt"),
    output:
        query_logcpm = os.path.join(OUT_DIR, "query_log2cpm.rds"),
    params:
        code_dir = CODE_DIR,
        out_dir = OUT_DIR,
    conda: INFERCNV_ENV,
    threads: 6
    resources:
        mem_mb = 48000,
        runtime = 120,
    log:
        os.path.join(LOG_DIR, "logcpm_pseudobulks.log")
    shell:
        """
        Rscript {params.code_dir}/construct_query_logcpm_pseudobulks.R \
            {input.query_pseudobulks} \
            {input.query_group_names} \
            {input.query_genes} \
            {params.out_dir} \
            2>&1 | tee {log}
        """

# ============================================================================
# Step 1.3: Create input gene sets
# ============================================================================

rule create_gene_sets:
    """Create input gene sets for model training."""
    input:
        tcga_xena = TCGA_XENA_PATH,
        tcga_clindata = TCGA_CLINDATA_PATH,
    output:
        gene_sets = expand(os.path.join(GENE_SETS_DIR, "{gene_set}.txt"), gene_set=GENE_SETS),
    params:
        code_dir = CODE_DIR,
        out_dir = GENE_SETS_DIR,
    conda: SNAPATAC2_ENV,
    threads: 6
    resources:
        mem_mb = 48000,
        runtime = 120,
    log:
        os.path.join(LOG_DIR, "create_gene_sets.log")
    shell:
        """
        python -u {params.code_dir}/create_input_gene_sets.py \
            {input.tcga_xena} \
            {input.tcga_clindata} \
            {params.out_dir} \
            2>&1 | tee {log}
        """

# ============================================================================
# Step 1.4: Train RNA models (one per gene set)
# ============================================================================

rule train_rna_model:
    """Train multiclass logistic regression model for LUAD vs LUSC classification."""
    input:
        tcga_xena = TCGA_XENA_PATH,
        tcga_clindata = TCGA_CLINDATA_PATH,
        gene_set = os.path.join(GENE_SETS_DIR, "{gene_set}.txt"),
    output:
        cv_metrics = os.path.join(OUT_DIR, "{gene_set}", "cv_metrics.pkl"),
        models = os.path.join(OUT_DIR, "{gene_set}", "models.pkl")
    params:
        code_dir = CODE_DIR,
        out_dir = os.path.join(OUT_DIR, "{gene_set}"),
    conda: SNAPATAC2_ENV,
    threads: 6
    resources:
        mem_mb = 48000,
        runtime = 120,
    log:
        os.path.join(LOG_DIR, "train_rna_{gene_set}.log")
    shell:
        """
        python -u {params.code_dir}/train_multiclass_logistic_regression_model.py \
            {input.tcga_xena} \
            {input.tcga_clindata} \
            {input.gene_set} \
            {params.out_dir} \
            --luad_vs_lusc \
            2>&1 | tee {log}
        """

# ============================================================================
# Step 1.5: Plot model performances
# ============================================================================

rule plot_model_performances:
    """Plot cross-validation metrics for all trained models."""
    input:
        cv_metrics = expand(os.path.join(OUT_DIR, "{gene_set}", "cv_metrics.pkl"), gene_set=GENE_SETS),
    output:
        plot = os.path.join(OUT_DIR, "all_models_cv_metrics.png"),
    params:
        code_dir = CODE_DIR,
        gene_sets_dir = GENE_SETS_DIR,
        models_dir = OUT_DIR,
        output_dir = OUT_DIR,
    conda: SNAPATAC2_ENV,
    threads: 6
    resources:
        mem_mb = 48000,
        runtime = 120,
    log:
        os.path.join(LOG_DIR,"plot_performances.log")
    shell:
        """
        python -u {params.code_dir}/plot_model_performances.py \
            {params.gene_sets_dir} \
            {params.models_dir} \
            {params.output_dir} \
            2>&1 | tee {log}
        """
